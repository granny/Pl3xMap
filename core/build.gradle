plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version "$shadowJarVersion"
}

group = "${rootProject.group}.core"
version = rootProject.version

base {
    archivesName = "${rootProject.name}-${project.name}"
}

java {
    withJavadocJar()
    withSourcesJar()
}

clean {
    delete("$projectDir/src/main/resources/web")
}

assemble {
    dependsOn(shadowJar)
    doLast {
        delete fileTree(dir: "$buildDir", include: '**/*-dev*.jar')
    }
}

shadowJar {
    archiveBaseName = "${rootProject.name}-${project.name}"
    archiveClassifier = ''
    mergeServiceFiles()
    exclude(
            'META-INF/LICENSE',
            'META-INF/LICENSE.txt'
    )
    [
            //'cloud.commandframework', // do not relocate
            'com.github.benmanes.caffeine.cache',
            'com.github.Carleslc.Simple-YAML',
            'com.google.errorprone.annotations',
            'com.luciad',
            //'io.leangen.geantyref', // do not relocate!
            'io.undertow',
            //'net.kyori', // do not relocate!
            'net.querz',
            'org.checkerframework',
            'org.jboss',
            'org.simpleyaml',
            'org.wildfly',
            'org.xnio',
            'org.yaml.snakeyaml',
    ].forEach { relocate it, "libs.$it" }
}

javadoc {
    options.encoding = 'UTF-8'
    options.addBooleanOption('html5', true)
    title = "${rootProject.name}-${project.version} API"
}

processResources {
    dependsOn ":copyWebmap"
}

sourcesJar {
    dependsOn ":copyWebmap"
}
